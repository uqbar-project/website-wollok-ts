---
title: Wollok Game
description: Hacer juegos en Wollok.
sidebar:
    order: 5
---

## ¬øQu√© es?

Wollok-Game es una biblioteca de Wollok que sirve para crear juegos. La idea que propone es tener una especie de "tablero" en donde se puedan ubicar los objetos de alg√∫n programa de Wollok y √©ste se mostrar√° autom√°ticamente en la pantalla.
A continuaci√≥n se describe c√≥mo utilizarlo y las funcionalidades que proporciona.

## El juego

El objeto m√°s importante para interactuar con Wollok Game es el objeto `game` que para poder utilizarlo hay que importar la biblioteca _game_ de Wollok.

## ¬øC√≥mo se usa?

### Por consola ###

La forma m√°s b√°sica es importar Wollok Game desde la consola (sin archivo asociado) haciendo

```wollok
wollok:example> import wollok.game.*
```

Y luego enviar los mensajes uno a uno, como habitualmente se usa la consola. 

```wollok
wollok:example> game.start()
üëæ Game available at: http://localhost:3001
üìÅ Project path: [/Users/fernando/workspace/pdp/wollokPdp/GameInit]
üóÇÔ∏è Assets folder: [assets]
üé® Assets []
‚úì true
```

En el puerto 3001 de nuestra m√°quina se levanta el motor de juegos. En un navegador podemos ver el tablero vac√≠o escribiendo en la URL: `http://localhost:3001`

<img src="/assets/doc/game/tableroVacio.png" alt="Tablero vac√≠o" width="50%" height="auto" class="img-fluid z-depth-1"/>

### Por consola, con archivo de c√≥digo ###

Una forma frecuente es usar un archivo .wlk y definir all√≠ el c√≥digo inicial como cualquier otro c√≥digo wollok v√°lido, hacerlo ejecutar y enviar los mensajes por consola para que se realicen las acciones deseadas. 

Archivo `example.wlk`

```wollok
import wollok.game.*

object juego {
  method iniciar(){
    game.start()
  }
}
```

Al ejecutar por consola este archivo, hacer

```wollok
wollok:example> juego.iniciar() 
```

### Con un programa

Otra forma de trabajar es creando un archivo `.wpgm`, o Wollok program, donde se define el c√≥digo del juego, que se ejecuta directamente al ejecutar el archivo, sin abrir la consola.

Archivo ejemplo.wpgm

```wollok
import wollok.game.*

program ejemplo {
  game.start()
}
```

## El tablero

Todo el mundo de Wollok Game pasa por el tablero. Aqu√≠ es donde se van a agregar los objetos que queremos mostrar en el juego y tiene las siguientes caracter√≠sticas:

- Es **√∫nico**, de modo que solo podemos tener un tablero por juego
- Todo el tablero **est√° dividido en celdas** _cuadradas_ que se puede configurar usando `game.cellSize(px)` (por default: 50 x 50 px), y todas las posiciones del juego est√°n en unidades de celdas (no se puede estar "en el medio" de una celda).
- Tiene un **t√≠tulo**, manejado por la propiedad `game.title()`
- Tiene un **ancho**, expresado en _cantidad de celdas_, manejado por la propiedad `game.width()`
- Tiene un **alto**, expresado en _cantidad de celdas_, manejado por la propiedad `game.height()`
- Tiene una **imagen de celda**, que se setea por medio de `game.ground(imagen)`, en donde `imagen` es un string con el nombre del archivo de la imagen.
 
### Ejemplo

```wollok
import wollok.game.*

program ejemplo {
  game.width(10)
  game.height(7)
  game.cellSize(50)
  game.title("Juego")
  game.start()
}
```

## Dibujando objetos

Ahora que sabemos c√≥mo ver el tablero del juego, vamos a agregar objetos visuales para que interact√∫en. Para dibujar alg√∫n objeto en Wollok Game es necesario saber _qu√© im√°gen_ dibujar y _d√≥nde en la pantalla_, para ello es necesario utilizar las **posiciones** y **visuales**.

### Las posiciones

Las **posiciones** son objetos que se encuentran en la biblioteca _game_ de Wollok y definen coordenadas x e y. La forma m√°s simple de obtener una posici√≥n es ped√≠rsela al **game** .

Por ejemplo, teniendo corriendo cualquier archivo `.wlk` que incluya el import de `wollok.game.*` se puede probar en al consola:

```wollok
wollok:example> game.at(2, 3)
‚úì 2@3
wollok:example> game.origin()
‚úì 0@0
wollok:example> game.center()
‚úì 2@2 // -> Se calcula a partir del height y width
```

Las posiciones entienden mensajes para cada coordenada

```wollok
wollok:example> game.at(2, 3).x()
‚úì 2
wollok:example> game.at(2, 3).y()
‚úì 3
```

Para dibujar un objeto en una posici√≥n es necesario que entienda el mensaje `position()` y que retorne la posici√≥n en la que se quiere mostrar al objeto. Dicho m√©todo puede ser tan complejo o simple como se desee: puede tener una l√≥gica espec√≠fica que calcule la posici√≥n a partir de diversos factores o ser simplemente un m√©todo de acceso a una variable, en cuyo caso basta con definir `position` como propiedad.

```wollok
// Con propiedad
object wollok {
  var property position = game.origin()
}

// Con m√©todo 
object wollok {
  var property centrado = false
  method position() = if (centrado) game.center() else game.origin()
}
``` 

Pero para dibujar un objeto no es suficiente con definir en d√≥nde mostrarlo. Tambi√©n debemos saber **qu√©** vamos a mostrar. Es decir, cu√°l ser√° la _imagen_ de nuestro objeto.

### Visuales

Para elegir la imagen de un determinado objeto es necesario:

1. Configurar cu√°l es la carpeta donde est√°n las im√°genes del juego. Por defecto en el `package.json` esa carpeta tiene como nombre `assets` pero se puede cambiar

```json
{
    "name": "tu-proyecto",
    "version": "1.0.0",
    "resourceFolder": "assets",
```

2. Agregar a tus objetos un m√©todo `image()` que **retorne el nombre del archivo de la imagen** como string, incluyendo la extensi√≥n. **Ten√© en cuenta que algunos sistemas operativos son case sensitive**, as√≠ que cheque√° bien may√∫sculas y min√∫sculas. **No es necesario definir la carpeta para las im√°genes, solo es necesario respetar el nombre del archivo de la imagen**.

#### Siguiendo con nuestro ejemplo anterior:

Su proyecto deber√≠a tener una estructura similar a la siguiente:

<div class="container text-center">
  <img src="/assets/doc/game/imgExplorerGame.png" class="img-fluid z-depth-1">
</div>

Y el c√≥digo del objeto a mostrar podr√≠a ser

```wollok
object personaje {
  var property position = game.origin()

  method image() = "personaje.png"
}
```

### Agregando objetos al juego

Por √∫ltimo, para que el objeto se muestre en el tablero de juego, se debe hacer

```wollok
import wollok.game.*

object personaje { ... }

program ejemplo {
  ...
  game.addVisual(personaje)
}
```

¬°Ya podemos visualizar nuestro personaje en pantalla!

<img src="/assets/doc/game/tableroConWollok.png" alt="Tablero con wollok" width="50%" height="auto" class="img-fluid z-depth-1"/>

As√≠ como existe `game.addVisual(objeto)`, tambi√©n se puede hacer `game.removeVisual(objeto)` cuando queremos dejar de tenerlo en el juego.

En `example.wlk`

```wollok
import wollok.game.*
object caja {
  method image() = "caja.png"
  method position() = game.center()
} 
```

### Y al ejecutar nuestro programa

```wollok
program ejemplo {
  ...
  game.addVisual(personaje)
  game.addVisual(caja)
}
```

<img src="/assets/doc/game/cajaGame.png" alt="Caja Game" width="50%" height="auto" class="img-fluid z-depth-1"/>

> ![Warning](/assets/doc/game/warning.png) AVISO IMPORTANTE : Wollok Game no hace ninguna modificaci√≥n a las im√°genes para mostrarlas. De modo que deber√°n tener el **tama√±o** y **orientaci√≥n** apropiado para tu juego.

-------

> **¬øC√≥mo funciona?**
> Wollok game le consultar√° a los objetos cu√°l es la posici√≥n e imagen para mostrarlos _todo el tiempo_ (muchas veces por segundo). Esto significa que para que un objeto "se mueva" o "transforme su imagen" basta con hacer que responda cosas distintas a esos mensajes.


## Moviendo objetos

Una forma para que el objeto se mueva en el tablero es definiendo adecuacadamente el m√©todo `position()` y manipulando las referencias que se utilizan en √©l. 
Las posiciones se pueden trabajar con **objetos mutables o inmutables**, en el primer caso se les puede cambiar sus coordenadas y en el segundo para ubicar objetos en posiciones diferentes se deben obtener nuevos objetos posici√≥n. 
En un caso simple, con una propiedad o un m√©todo que simplemente retorna la variable `position`, si modificamos la referencia a una posici√≥n diferente, el objeto se mueve a dicha ubicaci√≥n.

```wollok
// Con propiedad
object personaje {
  var property position = game.origin()

  method centrar() {
    position = game.center()
  }

  method image() = "personaje.png"
}

// Con m√©todo 
object personaje {
  var centrado = false
  method position() = if (centrado) game.center() else game.origin()

  method centrar() {
    centrado = true
  }

  method image() = "personaje.png"
}

``` 

Las posiciones entienden los mensajes `right(c) left(c) up(c) down(c)` que devuelven nuevas posiciones con un desplazamiento de `c` casilleros en la direcci√≥n correspondiente. 

```wollok
object personaje {
  var property position = game.origin()

  method image() = "personaje.png"

  method subir() {
    position = position.up(1) 
  }

  // se mueve una determinada cantidad de posiciones en diagonal principal
  method enDiagonal(cantidadPosiciones) { 
    position = position.up(cantidadPosiciones).right(cantidadPosiciones) 
  }

}
```

## El personaje

Wollok Game te permite tener un _personaje especial_ y le da la capacidad de **moverlo con las flechas del teclado**. Basta con decirle al juego cu√°l objeto es el personaje a la hora de dibujarlo. 
El objeto debe entender los mensajes `position()` y `position(nuevaPosition)`, lo que puede sustituirse definiendo `position` como propiedad. 

```wollok 
// en el programa
program ejemplo {
  ...
  game.start()
  game.addVisualCharacter(personaje)
  game.addVisual(caja)
}
```

![wollok-character](/assets/doc/game/wollokCharacter.gif)

## ¬°Tambi√©n hablan!

As√≠ es, **los objetos del juego pueden hablar**. Para eso hay que indicar el `objeto visual` del juego y el `texto` que dir√° por medio del mensaje `game.say(visual, texto)`:

```wollok
program ejemplo {
  ...
  game.say(personaje, "Se hace lo que se puede")
}
```

![Los objetos hablan](/assets/doc/game/objetosHablan.png)

## Un juego interactivo

Ya podemos mostrar nuestros objetos en el tablero, d√≥nde y con la imagen que queramos. Ahora falta poder interactuar con el juego para que sea divertido.

### Colisiones

Una forma de hacer que tus objetos interact√∫en entre s√≠ es por medio de colisiones. Estos son **bloques de c√≥digo** que se agregan a un objeto del tablero y se ejecutar√° cuando otro objeto _colisione_ con √©ste (ocupe la misma posici√≥n). `game.whenCollideDo(objeto, accionConColisionado)`.

#### Ejemplo

<img src="/assets/doc/game/colisionCaja.gif" alt="Colisi√≥n caja" width="50%" height="auto" class="img-fluid z-depth-1"/>

En `example.wpgm`

```wollok
import wollok.game.*

object personaje {
  var property position = game.origin()

  method image() = "personaje.png"
}

object caja {
  var property position = game.center()
  method image() = "caja.png"
  method subir(){
    position = position.up(1)
  }
}

program ejemplo {
  game.height(10)
  game.width(10)
  game.addVisualCharacter(personaje)
  game.addVisual(caja)

  // Cuando el personaje colisione con la caja, el personaje habla y la caja se desplaza
  game.whenCollideDo(personaje, { elemento =>
    elemento.subir()
    game.say(personaje, "Opa! Me llev√© puesta una caja!")
  })
  game.start()
}
```

**Tip:** si necesit√°s la imagen de la caja la pod√©s descargar en el directorio assets del ejemplo [sokoban](https://github.com/wollok/sokobanGame)

### Eventos autom√°ticos

Una funcionalidad interesante que podemos implementar es que **la caja se mueva cada n segundos** (por ejemplo, cada 2 segundos), enviando el mensaje `onTick()` al objeto `game`, el lapso de repetici√≥n expresado en milisegundos, un identificador (string descriptivo) y el bloque de c√≥digo que debe ejecutar:

```wollok
program ejemplo {
  game.addVisualCharacter(wollok)
  game.addVisual(caja)
  // cada dos segundos muevo la caja
  game.onTick(2000, "movimiento", { caja.movete() })
  //
  game.start()
}
```

El m√©todo `movete()` en caja actualiza la posici√≥n en base a un valor al azar, tomando en cuenta el ancho y alto del tablero:

```wollok
object caja {
  var property position = game.center()
  method image() = "caja.png"
  method movete() {
    const x = 0.randomUpTo(game.width()).truncate(0)
    const y = 0.randomUpTo(game.height()).truncate(0)
    // otra forma de generar n√∫meros aleatorios
    // const x = (0.. game.width()-1).anyOne() 
    // const y = (0.. game.height()-1).anyOne() 
    position = game.at(x,y) 
  }
}
```

Cuando ejecutamos el programa, vemos c√≥mo la caja cada 2 segundos cambia de posici√≥n (y nuestro personaje debe andar con m√°s cuidado):

<img src="/assets/doc/game/onTick.gif" alt="on tick" width="50%" height="auto" class="img-fluid z-depth-1"/>

¬øY si queremos definir un evento que suceda una sola vez? Tambi√©n podemos hacerlo, enviando el mensaje `schedule(miliseconds, action)` al objeto `game` de la siguiente manera:

```wollok
program ejemplo {
  ...
	game.schedule(3000, { game.say(personaje, "¬°Hola!") })
	game.start()
}
```

<img src="/assets/doc/game/schedule.gif" alt="schedule" width="50%" height="auto" class="img-fluid z-depth-1"/>

Veremos otras aplicaciones m√°s adelante.

### Eventos del teclado

Wollok Game permite capturar todos los eventos del teclado, para lo cual te recomendamos que leas la documentaci√≥n del objeto autodefinido `keyboard`. En particular, pod√©s definir qu√© tiene que pasar cuando un usuario presione una tecla. 
As√≠, un _ENTER_ podr√≠a hacer que el personaje salude, y la tecla _p_ que el movimiento de la caja se detenga.

```wollok
import wollok.game.*

program ejemplo {
  game.addVisualCharacter(wollok)
  game.addVisual(caja)
  
  game.onTick(2000, "movimiento", { caja.movete() })

  // capturamos el evento ENTER del teclado
  keyboard.enter().onPressDo { game.say(wollok, wollok.howAreYou()) }
  // capturamos el evento de presionar la tecla p del teclado
  keyboard.p().onPressDo { game.removeTickEvent("movimiento") }  // Mensaje que detiene la acci√≥n repetitiva indicada
  game.start()
}
```

<img src="/assets/doc/game/keyboardEvents.gif" alt="keyboard events" width="50%" height="auto" class="img-fluid z-depth-1"/>

<!-- ### Mostrar atributos de los objetos visuales ###

Incorporemos los siguientes atributos al objeto visual wollok:

```wollok
object personaje {
  var property position = game.origin()
  var version = "1.7.0"
  var lastUpdated = new Date()
  ...
}
```

Tambi√©n agregamos en el programa la configuraci√≥n para ver las referencias de los objetos visuales (por defecto esta opci√≥n est√° deshabilitada):

```wollok
program ejemplo {
  game.addVisualCharacter(personaje)
  game.showAttributes(personaje)
	// etc.
```

Esto hace que cuando paremos el mouse sobre dicho objeto, veamos su estado interno:

![attributes](/assets/doc/game/attributes.png)

Si no queremos deshabilitarlo una vez activado (porque nos distrae tanta informaci√≥n en el tablero), podemos enviar el mensaje `hideAttributes()` a `game` indicando cu√°l es el objeto visual al que no queremos que se le vean los atributos:

```wollok
program ejemplo {
  game.addVisualCharacter(wollok)
  //...
  game.hideAttributes(wollok)
}
``` -->

## Cambiando el fondo ##

Es posible modificar el fondo de nuestro tablero, para lo cual pod√©s buscar cualquier imagen _libre de derechos de autor_ que te guste. La descargamos en la carpeta de _assets_ de nuestro proyecto.

En el programa agregamos el mensaje correspondiente al objeto game:

```wollok
import wollok.game.*

program ejemplo {
  game.width(10)
  game.height(10)
  game.boardGround("playa.jpg") // o el nombre con el que lo hayas bajado
  game.start()
}
```

Esto produce que en el tablero se visualice la imagen de fondo: 

<img src="/assets/doc/game/tableroConFondoEspecial.png" alt="tablero con fondo especial" width="50%" height="auto" class="img-fluid z-depth-1"/>

De aqu√≠ en m√°s volveremos con el fondo convencional para que distraiga menos la atenci√≥n.

## Dibujando textos

¬øSe acuerdan que dijimos que para dibujar un objeto era necesario definir su imagen? Bueno, esto no es del todo cierto. Es posible mostrar solamente texto. 

Para ello es necesario agregarle a nuestro objeto un m√©todo `text()` **que devuelva el texto a mostrar** como string. Es importante definir la posici√≥n en la cual se debe mostrar de la misma manera que lo hac√≠amos antes. El objeto puede no tener el m√©todo `image()`.

### Ejemplo

```wollok
import wollok.game.*

object pepita {
	method position() = game.center()
	method text() = "¬°Pepita!"
}
```

Al correr este programa **nuevo**:

```wollok
object pepita {
  method position() = game.center()
  method text() = "¬°Pepita!"
}

program otroEjemplo {
	game.addVisual(pepita)
	game.start()
}
```

Deber√≠amos poder ver el texto _¬°Pepita!_ escrito en el medio del tablero:

<img src="/assets/doc/game/pepitaText.png" alt="Pepita text" width="50%" height="auto" class="img-fluid z-depth-1"/>

El color por defecto es **azul**, pero se puede modificar.

### Coloreando textos

Para poder cambiar el color del texto debemos agregarle a nuestro objeto un m√©todo `textColor()` que debe devolver un string con un valor RGBA en hexa.

¬øQu√© es un valor RGBA? Es una forma de representar colores mediante la composici√≥n de rojo, verde y azul. Adem√°s, se agrega informaci√≥n sobre la opacidad. Para m√°s informaci√≥n pueden visitar [esta p√°gina](https://rgbacolorpicker.com/). Explica con m√°s detalle de qu√© se trata y adem√°s les genera un valor RGBA a partir del color que ustedes quieran.

¬øC√≥mo lo convierto a hexa? Muy f√°cil. Pueden utilizar cualquier p√°gina que, dado un valor RGBA, lo convierta en hexa. Les dejamos [esta p√°gina](https://rgbatohex.com/) como ejemplo.

### Ejemplo

```wollok
import wollok.game.*

object paleta {
	const property verde = "009933FF"
	const property rojo = "FF0000FF"
}

object pepita {
	method position() = game.center()
	method text() = "¬°Pepita!"
	method textColor() = paleta.verde()
}
```

Corriendo el mismo programa anterior deber√≠amos poder ver:

<img src="/assets/doc/game/greenPepitaText.png" alt="Green pepita text" width="50%" height="auto" class="img-fluid z-depth-1"/>

### Im√°genes con texto

¬°Podemos combinar textos con im√°genes! Para ello necesitamos que nuestro objeto defina qu√© _imagen_ va a mostrar, de la misma manera que ven√≠amos haciendo antes. El texto siempre se dibujar√° por encima de la imagen.

### Ejemplo

```wollok
import wollok.game.*

object paleta {
	const property verde = "009933FF"
	const property rojo = "FF0000FF"
}

object pepita {
	method position() = game.center()
	method image() = "pepita.png"
	method text() = "¬°Pepita!"
	method textColor() = paleta.verde()
}
```

Corriendo el mismo programa anterior deber√≠amos poder ver:

<img src="/assets/doc/game/pepitaWithText.png" alt="Pepita with text" width="50%" height="auto" class="img-fluid z-depth-1"/>

## Objetos invisibles

Tambi√©n es posible definir objetos invisibles y agregarlos al tablero. Nos pueden servir, por ejemplo, para disparar colisiones. Es importante que estos objetos **no definan** los m√©todos `text()` ni `image()`.

### Ejemplo

Creamos un archivo `pepita.wlk`:

```wollok
import wollok.game.*

object pepita {
	var property position = game.center()
	method image() = "pepita.png"
}

object invisible {
	method position() = game.origin()
}
```

Al correr el siguiente programa:

```wollok
import wollok.game.*
import pepita.*

program ejemplo {
	game.addVisualCharacter(pepita)
	game.addVisual(invisible)
	game.onCollideDo(invisible, {elemento => game.say(invisible, "¬°Cuidado!")})
	game.start()
}
```

Deber√≠amos poder observar que cuando pepita pasa por el origen, el objeto invisible le dice: ¬°Cuidado!

<img src="/assets/doc/game/invisibleObject.gif" alt="Invisible object" width="50%" height="auto" class="img-fluid z-depth-1"/>

## Sonidos

¬°Podemos reproducir sonidos! Para ello podemos pedirle un sonido a game, envi√°ndole el mensaje: `game.sound(audioFile)`. El par√°metro es el path al archivo de audio que quieren reproducir. Las extensiones aceptadas son: *.mp3, .ogg o .wav*.

Al igual que las im√°genes, podemos guardar nuestros sonidos dentro de la carpeta `assets`.

¬øQu√© podemos hacer con un sonido? Podemos reproducirlo envi√°ndole el mensaje `play`.

### Ejemplo

```wollok
import wollok.game.*

object waterDrop {
	
	method play(){
		game.sound("water-drop-sound.mp3").play()
	}
}

program soundProgram {

	keyboard.enter().onPressDo({waterDrop.play()})
	game.start()

}
```

Cada vez que apretemos la tecla `enter` se reproducir√° nuestro sonido.

**Importante:** los sonidos pueden reproducirse s√≥lo una vez. Si queremos hacerlo de nuevo es necesario crear otro sonido.

### Sonido de fondo

Tambi√©n podemos definir m√∫sica de ambiente o un sonido de fondo para el juego. Esto lo logramos envi√°ndole el mensaje `shouldLoop(true)` al sonido.


```wollok
import wollok.game.*

program soundProgram {
	
	const rain = game.sound("light-rain.mp3")
	rain.shouldLoop(true)
	game.schedule(500, { rain.play()} )
	game.start()
}
```

**Importante:** los sonidos s√≥lo pueden reproducirse si el juego ya inici√≥. Es por eso que debemos *planificar* su reproducci√≥n si queremos que suceda de manera autom√°tica al iniciar el juego.

### Pausar

Otras cosas que podr√≠amos querer hacer con los sonidos es pausarlos, reanudarlos y detenerlos por completo. Para ello existen los mensajes `pause()`, `resume()` y `stop()` que entienden los sonidos.

```wollok
import wollok.game.*

program soundProgram {
	
	const rain = game.sound("light-rain.mp3")
	rain.shouldLoop(true)
	keyboard.p().onPressDo({rain.pause()})
	keyboard.r().onPressDo({rain.resume()})
	keyboard.s().onPressDo({rain.stop()})
	game.schedule(500, {rain.play()})
	game.start()
}
```

Esto nos permite *pausar* la lluvia con la letra **p**, volver a *reproducirla* con la letra **r** y *detenerla* con la letra **s**.

### Volumen

Por √∫ltimo quer√≠amos mostrarles que los sonidos tambi√©n tienen su propio volumen y son independientes unos de otros. Podemos consultarlo enviando el mensaje `volume()` a un sonido y tambi√©n podemos modificarlo si as√≠ lo deseamos, mediante `volume(newVolume)`.

```wollok
import wollok.game.*

program soundProgram {
	
	const rain = game.sound("light-rain.mp3")
	rain.shouldLoop(true)
	keyboard.up().onPressDo({rain.volume(1)})
	keyboard.down().onPressDo({rain.volume(0)})
	keyboard.m().onPressDo({rain.volume(0.5)})
	game.schedule(500, {rain.play()})
	game.start()
}
```

En el ejemplo mostramos c√≥mo podemos *mutear* un sonido presionando la tecla **down**, llevarlo a su m√°ximo volumen con la tecla **up** y dejarlo en un valor intermedio presionando la tecla **m**.

## Testing

En este apartado mostraremos algunas consideraciones que deber√°n tener en cuenta a la hora de hacer tests con `wollok game`. Pero antes de comenzar, les recomendamos que lean los apuntes sobre testing: [Introducci√≥n al testeo unitario automatizado](https://docs.google.com/document/d/1Q_v48gZfRmVfLMvC0PBpmtZyMoALbh11AwmEllP__eY/edit?usp=drive_web) y [Testeo unitario automatizado avanzado](https://docs.google.com/document/d/1caDE_mlP1QMfzyVpyvh-tKshjAeYLXBkXDYrTX5zFUI/edit#?usp=drive_web).

### Game mantiene su estado

Es probable que quieran agregar elementos al tablero para probar cierta funcionalidad mediante su interacci√≥n o movimiento. Pero, si agregan un elemento en un test, este mismo vivir√° en los dem√°s. Esto es problem√°tico y puede romper sus tests.

Para solucionarlo pueden enviarle el mensaje `clear()` al objeto `game`, de manera que el juego se limpie y quede libre de objetos. Pero, ¬ød√≥nde har√≠amos eso? Dentro del m√©todo `initialize()` del `describe`. De esta manera, nos aseguramos que `game` se reinicie antes de correr cada uno de nuestros tests.

```wollok
import wollok.game.*

describe "Mi describe" {

  method initialize(){
    game.clear()
    // Hago otras cosas...
  }

  test "Mi primer test" {
    game.addVisual(miVisual)
    ...
  }

  test "Mi segundo test"{
    game.addVisual(otroVisual)
    ...
  }
}
```

### Reproducci√≥n de sonidos

Ya vimos que los sonidos no se pueden reproducir si el juego no empez√≥. Y en los tests no nos interesa iniciar el juego. Entonces, ¬øqu√© sucede si alguna funcionalidad que queramos testear reproduce un sonido por atr√°s? Se lanza una excepci√≥n y el test falla.

Solucionar √©sto es un poco m√°s complicado porque requiere de varios cambios. En un principio, el problema empieza cuando le enviamos el mensaje `play()` a un sonido. Lo que podr√≠amos hacer es cambiar estos sonidos por otros objetos polim√≥rficos que los "imiten". Es decir, estos nuevos objetos entender√°n todos los mensajes que entienden los sonidos y cuando les mandemos el mensaje `play()` no har√°n nada. De esta manera no se lanzar√° una excepci√≥n y los test podr√°n pasar.

```wollok
object soundMock {
	
	method pause(){}
	
	method paused() = true
	
	method play(){}
	
	method played() = false
	
	method resume(){}
	
	method shouldLoop(looping){}
	
	method shouldLoop() = false
	
	method stop(){}
	
	method volume(newVolume){}
	
	method volume() = 0
}
```

Pero los sonidos se los pedimos al objeto `game()`. Entonces, tenemos que cambiar el objeto al cual le pedimos los sonidos. Vamos a crear un nuevo objeto que se va a encargar de crearlos por nosotros. Lo llamaremos `soundProducer`. Dicho objeto, en un principio, se lo pedir√° a `game` porque necesitamos los sonidos de verdad. Lo interesante es que vamos a poder intercambiar a qui√©n le pide los sonidos. Entonces, podemos tener otro objeto que nos de los sonidos "de mentira" para poder hacer nuestros tests. A este √∫ltimo lo llamaremos `soundProviderMock`. Bajando esto a tierra, podr√≠amos tener algo como lo siguiente:

```wollok
import wollok.game.*

object soundProducer {
	
	var provider = game
	
	method provider(_provider){
		provider = _provider
	}
	
	method sound(audioFile) = provider.sound(audioFile)
	
}

object soundProviderMock {
	
	method sound(audioFile) = soundMock
	
}
```

En nuestro c√≥digo vamos a tener que modificar todos los lugares donde le ped√≠amos un sonido a `game`. Es decir, reemplazaremos `game.sound(audioFile)` por `soundProducer.sound(audioFile)` (o el nombre que ustedes hayan elegido).

¬øY qu√© ganamos con esto? En el `describe` de nuestros tests vamos a poder configurar el proveedor de sonidos dentro del m√©todo `initialize()`, similar al caso anterior. Esto nos permitir√° usar sonidos "de mentira" para que no rompan nuestros tests.

```wollok
describe "Mi describe" {

  method initialize(){
    soundProducer.provider(soundProviderMock)
    // Hago otras cosas...
  }

  test "Mi primer test" {
    ...
  }
}
```

## Reportando errores

Cuando las cosas no salen como queremos y ocurre un error, el personaje especial es el que nos lo cuenta. Supongamos que modificamos el bloque que trabaja la colisi√≥n entre nuestro personaje wollok y la caja:

```wollok
program ejemplo {
  game.addVisualCharacter(personaje)  
  game.addVisual(caja)
  game.errorReporter(personaje)
  game.whenCollideDo(wollok, { elemento => 
    throw new DomainException(message = "No puede ser!")
  })
  game.start()
}
```

Cuando tiramos un error de dominio (DomainException) el personaje nos lo informa:

<img src="/assets/doc/game/errorReporter1.gif" alt="personaje reporta error" width="50%" height="auto" class="img-fluid z-depth-1">

Otros tipos de error (como `1 / 0` o tirar otro tipo de excepci√≥n) solo se loguean en la consola.

<!-- Por defecto, si hay un personaje es √©ste el responsable de avisar los errores que ocurren en el juego, y en caso de no haber personaje ser√° cualquier objeto visual que est√© en el tablero. -->

Nosotros podemos decirle que otro sea el objeto que reporte los errores de Wollok Game, como la caja:

```wollok
program ejemplo {
  //...
  game.errorReporter(caja)
  //...
}
```

Entonces cuando hay un error en un bloque de c√≥digo que maneja Wollok Game, ser√° la caja la que nos est√© reportando un error:

<img src="/assets/doc/game/errorReporter2.gif" alt="caja reporta error" width="50%" height="auto" class="img-fluid z-depth-1">


## Para seguirla ##

El c√≥digo de los ejemplos mencionados est√° en [este repositorio](https://github.com/wollok/ejemploGameSite).

Tambi√©n pod√©s investigar [todos estos ejemplos](https://github.com/wollok?utf8=%E2%9C%93&q=game&type=&language=) en el repositorio de Wollok. ¬°Que lo disfrutes!
